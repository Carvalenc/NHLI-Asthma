global data "D:\BDAU\Data_Aurum\"
global raw "D:\BDAU\Raw_Aurum\"

* matching cohort

************************************************
* create controls using denominator file
use ${data}Denominator_patients_GOLD_May2020, clear
merge 1:1 patid using "D:\D drive\STUDIES\Asthma_CVS_prevalence\BDAU\Denominator_acceptablepatients_GOLD_May2020.dta", keep(match) nogen
keep if accept==1
drop accept

merge 1:1 patid using ${data}LinkageEligibility_GOLD, keep(match) nogenerate
keep if death_e == 1 & hes_e==1 & lsoa_e==1

keep if gender == 1 | gender == 2

merge m:1 pracid using ${data}Denominator_practices_GOLD_May2020, keep(match) nogen
dateconvert linkdate lcd uts crd tod deathdate

drop mob marital famnum regstat reggap internal toreason frd linkdate hes_e death_e cr_e lsoa_e mh_e region 

gen start = date("01/01/2004", "DMY")

* date HES & ONS linked until
gen end = date("5/1/2019", "DMY")

* as over 18 cant use mob
gen year18 = yob + 18
gen age18 = date("15/07/"+string(year18), "DMY")
format age18 %td
drop year18

gen startstudy = max(uts, crd, start, age18)

gen endstudy = min(deathdate, end, lcd, tod)

drop if endstudy<=startstudy
keep patid gender pracid startstudy endstudy yob

*Set up file for use with 'getmatchedcohort'
gen indexdate = .
rename pracid practice
rename startstudy startdate
rename endstudy enddate
gen exposed = 0

keep patid practice indexdate gender startdate enddate exposed yob
order patid practice indexdate gender startdate enddate exposed yob

save ${data}Controls_GOLD, replace


******* get exposed ready ***************
use ${data}Asthma_PATID_LIST, clear
keep patid pracid gender dob studystart studyend 
gen yob=year(dob)
drop dob

*Set up file for use with 'getmatchedcohort'
* here I think index date is just studystart as not doing from day of diagnosis nor including them as controls before diagnosis incase delayed diagnosis but had it
gen indexdate = studystart
rename pracid practice
gen exposed = 1
rename studystart startdate
rename studyend enddate


********************** match them ***********************

*IMPORTANT NOTE: in most cases it is desirable to allow exposed patients to be available as controls prior to their date of first exposure. Such patients should be included in the dataset twice (i.e. two separate rows): once as exposed (exposed = 1, startdate = start of CPRD follow-up, indexdate = date of first exposure, enddate = end of CPRD follow-up), and once as a potential control (exposed = 0, startdate = start of CPRD follow-up, indexdate = missing, enddate = date of first exposure-1)

** In this senario I have not done this as may well be had asthma before oficial diagnosis SO I have not done the step where set cases as eligible controls before become cases - see Eleanor's dofile

* IMPORTANT NOTE 2: It is recommended to set the random number seed before running this program, so that the matching can be reproduced (see example below)

append using ${data}Controls_GOLD

set seed 4006

/* Options used:
	- Match on practice, gender, and age [yob] & allow matches with ages within +/- 1 years of case (5-year age bands) [yobwindow]
	- Match 1:1 controls:cases [ctrlsperexp]
	- Update every 100 matches [updates]
	- Save in specified location [savedir & filesuffix]
	- Don't ask me to confirm things, just run [dontcheck] */
	
	
/*	
(421,814 observations created)
(412,610 real changes made)
  421,814

Warning: no matches at all found for 1011 exposed patients
(note: variable exposed was byte, now float to accommodate using data's values)

    Result                           # of obs.
    -----------------------------------------
    not matched                             0
    matched                           843,628  
    -----------------------------------------
(421814 real changes made)    */

getmatchedcohort, practice gender yob yobwindow(1) followup ctrlsperexp(1) ///
	updates(1000) savedir(D:\BDAU\Data\) ///
	filesuffix(matched_cohort_GOLD) dontcheck
	
use ${data}getmatchedcohortcrd_matched_cohort_GOLD, clear

*Count number of cases that found matches
sort setid
by setid: gen litN = _N
codebook patid if exposed == 1

*Count number of cases that found < 2 matches
tab litN 
* drop if litN<2
drop lit

gen fu=(end-index)/365.25
sum fu, det

merge m:1 patid using "D:\BDAU\Data\Asthma_PATID_LIST.dta", keepusing(age gender) nogen keep(master match)
rename gender sex
merge m:1 patid using "D:\BDAU\Data\Denominator_patients_GOLD_May2020.dta", keepusing(yob gender) nogen keep(master match)
replace gender=sex if gender==.
drop sex

gen dob=date("15/07"+ string(yob), "DMY")
replace age_start=(index-dob)/365.25 if age_start==.
drop dob yob

save ${data}getmatchedcohortcrd_matched_cohort_GOLD, replace

********************************************************************************
************************** AURUM ****************************
********************************************************************************

***********************
* unexposed using denominator
use ${raw}Denominator_Aurum_Patients_May2020, clear
merge m:1 pracid using ${raw}Denominator_Aurum_AllPractices_May2020, nogen
dateconvert regstartdate regenddate cprd_ddate lcd

* aurum doesnt have uts (for now)
drop uts region

merge 1:1 patid using ${data}LinkageEligibility_AURUM, keep(match) nogenerate
keep if death_e == 1 & hes_e==1 & lsoa_e==1

gen start = date("01/01/2004", "DMY")

* date HES & ONS linked until
gen end = date("5/1/2019", "DMY")

* as over 18 cant use mob
gen year18 = yob + 18
gen age18 = date("15/07/"+string(year18), "DMY")
format age18 %td
drop year18

gen startstudy = max(regstart, start, age18)

gen endstudy = min(cprd_ddate, end, lcd, regend)

drop if endstudy<=startstudy
keep patid gender pracid startstudy endstudy yob

*Set up file for use with 'getmatchedcohort'
gen indexdate = .
rename pracid practice
rename startstudy startdate
rename endstudy enddate
gen exposed = 0

order patid practice indexdate gender startdate enddate exposed yob
drop if gender==.

save ${data}Controls_AURUM, replace

* to overcome issue of Aurum data need to give every patient patid and pracid like GOLD has  i.e. patid is ID followed by practid ID 
* first need to create new pracid for each GP practice in Aurum
use "D:\BDAU\Data\Controls_AURUM.dta", clear
keep practice
duplicates drop
gen pracid=_n
save  "D:\BDAU\Data\Practice_Pracid_Aurum.dta", replace

use "D:\BDAU\Data\Controls_AURUM.dta", clear
merge m:1 practice using "D:\BDAU\Data\Practice_Pracid_Aurum.dta"
bysort pracid: gen id=_n
tostring id, generate(id2)
tostring pracid, generate(pracid2)
gen zero="00"
gen pracid3=zero+pracid2 if pracid<10
gen zero1="0"
gen pracid4=zero1+pracid2 if (pracid>9 & pracid<100)
replace pracid2=pracid4 if pracid4!=""
replace pracid2=pracid3 if pracid3!=""
drop pracid id2 zero pracid3 zero1 pracid4 
tostring id, generate(id2)
gen id3=id2+pracid2
drop id id2 
destring id, generate(new_patid)
destring pracid, generate(new_pracid)
drop pracid2 id3
rename patid old_patid
rename practice old_pracid
rename new_pracid practice
rename new_patid patid
order patid practice
save "D:\BDAU\Data\Controls_AURUM_NewID_AllIN.dta", replace

**************************
* exposed
use ${data}20_090_Aurum_01_Extract_PatientPracid_Modified_All, clear
gen yob=year(dob)
drop dob fu age

keep patid gender pracid studystart studyend yob

gen indexdate = studystart
gen exposed = 1
rename studystart startdate
rename studyend enddate

rename patid old_patid
rename pracid old_pracid

* get new id's 
merge m:1 old_patid using "D:\BDAU\Data\Controls_AURUM_NewID_AllIN.dta", keep(match) nogenerate
drop old_patid old_pracid 


order patid practice indexdate gender startdate enddate exposed yob

save   "D:\BDAU\Data\20_090_Aurum_01_Extract_PatientPracid_Modified_All_NewID.dta", replace


*******************************
* match them

append using "D:\BDAU\Data\Controls_AURUM_NewID_AllIN.dta"

set seed 4006

getmatchedcohort, practice gender yob yobwindow(1) followup ctrlsperexp(1) ///
	updates(100) savedir(D:\BDAU\Data\) ///
	filesuffix(matched_cohort_AURUM) dontcheck

use	"D:\BDAU\Data\\getmatchedcohortmatched_cohort_AURUM.dta", clear

*Count number of cases that found matches
sort setid
by setid: gen litN = _N
codebook patid if exposed == 1

*Count number of cases that found < 2 matches
tab litN 
* drop if litN<2
drop lit

* convert back to Aurum format
use "D:\BDAU\Data\\getmatchedcohortmatched_cohort_AURUM.dta", clear
rename index index_match
rename end end_match
rename exp exp_match
merge m:1 patid using "D:\BDAU\Data\Controls_AURUM_NewID_AllIN.dta", keep(match)  nogenerate
keep setid exp_match index_match end_match old_patid old_pracid gender yob 
order setid old_pat old_pracid
rename old_patid patid
rename old_pracid pracid
rename exp exposed


gen fu=(end-index)/365.25
sum fu, det

gen dob=date("15/07"+ string(yob), "DMY")
gen age_start=(index-dob)/365.25
drop dob yob

save ${data}getmatchedcohort_matched_cohort_AURUM_final, replace

keep if exp==0
* 578,250  patients
save ${data}getmatchedcohort_matched_cohort_AURUM_final_UnexposedOnly, replace

use ${data}getmatchedcohort_matched_cohort_AURUM_final, clear
keep if exp==1
*  578,250  patients
save ${data}getmatchedcohort_matched_cohort_AURUM_final_ExposedOnly, replace
